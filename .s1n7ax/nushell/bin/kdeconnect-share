#!/usr/bin/env nu

source ~/.s1n7ax/nushell/utils/file.nu

use file_util get-files-from-paths

def main [...paths: string] {
  let files = get-files-from-paths $paths

  if ($files | length) < 1 {
    error make {
      msg: ("No files passed!

      ex:-
      command ~/directory ~/file.txt" | lines | str trim | str collect "\n")
    }
  }

  let available_devices = (kdeconnect-cli -a --name-only | lines)
  let device_count = ($available_devices | length)

  if $device_count < 1 {
    error make {
      msg: "No devices connected to KDEConnect at the moment"
    }
  }

  if $device_count == 1 {
    $files
    | each {|file|
        kdeconnect-cli $"--name=($available_devices | first)" $"--share=($file)"
      }

    exit
  }

  let selected_device = ($available_devices | str collect "\n" | dmenu | str trim)

  $files
  | each {|file|
      kdeconnect-cli $"--name=($selected_device)" $"--share=($file)"
    }
}
